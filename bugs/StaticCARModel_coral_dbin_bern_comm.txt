
    
    model{
    
    ## priors 
    for (k in 1:nspec) {
       # CAR prior distribution for spatial random effects:
       rho[k,1:nsite] ~ car.normal(adj[], weights[], num[], spacetau[k])
       # hyperprior of rho 
       spacesigma[k] ~ dunif(0,5)
       spacetau[k]  <- 1/(spacesigma[k]*spacesigma[k])
    }

    
    # regression parameters
    for (k in 1:nspec) {
       b0[k] ~ dunif(-10,10)
       ## detection probability
       p[k] ~ dunif (0,1)
    }
    
    ## likelihood
    for (k in 1:nspec) { # for each species
       for(i in 1:nsite) { # for each site
          ## occupancy model
          z [i,k] ~ dbern (psi[i,k])

          mu[i,k] <- b0[k] + rho[k,i]
          mu.lim[i,k] <- min(10,max(-10,mu[i,k]))
          logit(psi[i,k]) <- mu.lim[i,k]

          ## observation model 
          C[i,k] ~ dbin(p.eff[i,k],N[i])
          p.eff[i,k] <- p[k] * z[i,k]
       
          }## close site loop
       } ## close species loop

   
    # Derived parameters
    # number of species per site (finite sample size)
    for (i in 1:nsite) {
      n.occ [i] <- sum(z[i,]) 
      mutot [i] <- sum(psi[i,])
    }
    
    # number of sites per species 
    for (k in 1:nspec) {
      n.spp[k] <- sum(z[,k])
      n.spp.mu[k] <- sum(psi[,k])
    }

    # mean detection probabability
    meanP <- mean(p[])
    meanPsi <- mean(psi[,])
    meanZ <- mean(z[,])
}
    
